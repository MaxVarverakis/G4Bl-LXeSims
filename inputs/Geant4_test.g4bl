# Redirect stdout & stderr to a file
output g4bl.out

# QGSP is the "default" physics use-case for HEP, but obsolete
# FTFP_BERT : For calorimetry, colliders, and general HEP
physics FTFP_BERT

###########################################################
# * Not needed? *
# the beam is 3 GeV kinetic energy, the mass of an e- is 0.511 MeV/c^2
#param M=0.511 KE=3000.0
#param P=sqrt(($M+$KE)*($M+$KE)-$M*$M)
###########################################################

# LXe parameters

# 10 cm ~ 3.5 Radiation lengths of LXe
param RL=28.72 # radiation lenth of LXe in mm
param L_RL=3.5 # thickness in radiation lengths
param w=100 #  total x- and y-dimension [mm]
param LXeZ='$RL * $L_RL' # total z-dimension [mm]

# offset the LXe target in z
param LXeFront='$LXeZ/2' # LXe z offset (mm)

# Beam parameters
param P=3000.0 # 3 GeV/c
param sigmaR=3.0 # mm
param beamX=0 # mm
param beamY=0 # mm

# Window parameters
param WinThickness=0.5 # mm
param Rmax=10 # total radius of Be window (mm)
param mat=Be # define the window material as separate parameter so it can be easily changed

param WinInZ='-$WinThickness/2'
param WinOutZ='$LXeZ+$WinThickness/2'


# Set e- beam parameters
beam gaussian meanMomentum=$P nEvents=1000 particle=e- beamZ=-1000.0 beamX=$beamX beamY=$beamY sigmaX=$sigmaR sigmaY=$sigmaR sigmaXp=0.000100 sigmaYp=0.000100


###########################################################
#                       Detectors                         #
###########################################################

# For LXe target
detector Det width=$w height=$w length=$LXeZ material=lXe color=0,0,0 coordinates=global format=ASCII

# For windows
detector Win radius=$Rmax length=$WinThickness material=$mat color=1,0,0,1 coordinates=centerline format=ASCII

# Solely for collecting particle data at the end of the LXe target and exit window
# no `place` needed
zntuple zDet z='$LXeFront+$LXeZ/2' format=ASCII coordinates=centerline file='zDet.txt'
zntuple EntranceWinDet z='$WinInZ-$WinThickness/2' format=ASCII coordinates=centerline file='EntranceWinDet.txt'
zntuple zWinDet z='$WinOutZ+$WinThickness/2' format=ASCII coordinates=centerline file='zWinDet.txt'


###########################################################
#                   Detector Placement                    #
###########################################################

# Place LXe
place Det x=$beamX y=$beamY z=$LXeFront color=0,0,1,.8 rename=LXe perTrack=1

# Place windows
place Win x=$beamX y=$beamY z=$WinInZ rename=WinIn perTrack=1
place Win x=$beamX y=$beamY z=$WinOutZ rename=WinOut perTrack=1

# Test vacuum detector
#detector Det width=100 height=100 length=1 material=Vacuum color=0,1,0,.85 coordinates=global format=ASCII
#place Det x=$beamX y=$beamY z=-10 rename=VacDet perTrack=1