param maxStep=0.1 minStep=0.001 zTolerance=0.1 eventTimeLimit=10000
# ^ units:    mm            mm              mm                    s

# Redirect stdout & stderr to a file
output g4bl.out

# QGSP is the "default" physics use-case for HEP, but obsolete
# FTFP_BERT : For calorimetry, colliders, and general HEP
physics FTFP_BERT # minRangeCut=1.E-3

###########################################################
# * Not needed? *
# the beam is 3 GeV kinetic energy, the mass of an e- is 0.511 MeV/c^2
#param M=0.511 KE=3000.0
#param P=sqrt(($M+$KE)*($M+$KE)-$M*$M)
###########################################################

# LXe parameters

param w=70 #  total x- and y-dimension (mm)
param d=0.1 # slice thickness for each detector (mm)

# 10 cm ~ 3.5 Radiation lengths of LXe
param LXeZ=100 # total z-dimension [mm]
param zDiv='$LXeZ/$d-1'

# offset the LXe target in z
param LXeFront=$LXeZ/2 # LXe z offset (mm)

# Beam parameters
param P=3000.0 # 3 GeV/c
param sigmaR=3.0 # mm
param beamX=0 # mm
param beamY=0 # mm

# Window parameters
param WinThickness=2.0 # mm
param Rmax=10.0 # total radius of window (mm)
param mat=Al # define the window material as separate parameter so it can be easily changed

param WinInZ='-$WinThickness/2'
param WinOutZ='$LXeZ+$WinThickness/2'


# Set e- beam parameters
beam gaussian meanMomentum=$P nEvents=1000 particle=e- beamZ=-1000.0 beamX=$beamX beamY=$beamY sigmaX=$sigmaR sigmaY=$sigmaR sigmaXp=0.000100 sigmaYp=0.000100


###########################################################
#                       Detectors                         #
###########################################################

# For LXe target
detector Det width=$w height=$w length=$d material=lXe color=0,0,0 coordinates=global format=ASCII

# For windows
detector Win radius=$Rmax length=$WinThickness material=$mat color=1,0,0,1 coordinates=centerline format=ASCII

# Solely for collecting particle data at the end of the LXe target and exit window
# no `place` needed
zntuple zDet z='$LXeFront+$LXeZ/2' format=ASCII coordinates=centerline file='zDet.txt'
zntuple EntranceWinDet z='$WinInZ-$WinThickness/2' format=ASCII coordinates=centerline file='EntranceWinDet.txt'
zntuple zWinDet z='$WinOutZ+$WinThickness/2' format=ASCII coordinates=centerline file='zWinDet.txt'


###########################################################
#                   Detector Placement                    #
###########################################################

# Place z-slices of detectors to fill out the LXe target
do k 0 $zDiv
    param z='($k+1/2)*$d'
    param color=.5,0,$k/$zDiv,.8
    place Det x=$beamX y=$beamY z=$z color=$color perTrack=1 rename='Det$k'
enddo
ntuple DetsAll detectors=Det* file=LXe union=1 format=ASCII

# Place windows
place Win x=$beamX y=$beamY z=$WinInZ rename=WinIn perTrack=1
place Win x=$beamX y=$beamY z=$WinOutZ rename=WinOut perTrack=1